@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Purchase Requisition Header'
@ObjectModel.supportedCapabilities: [ #OUTPUT_FORM_DATA_PROVIDER ]
@Metadata.allowExtensions: true
define root view entity ZMM_C_PrHeader
  provider contract transactional_query
  as projection on ZMM_I_PrHeader
{
  key     PurchaseRequisition,
          PurReqnDescription,
          PurchaseRequisitionType,
          LastChangeDateTime,
          @ObjectModel.virtualElementCalculatedBy: 'ABAP:ZCL_MM_PR_FORM'
  virtual FileName   : abap.char( 50 ),
          @ObjectModel.virtualElementCalculatedBy: 'ABAP:ZCL_MM_PR_FORM'
          @Semantics.mimeType: true
  virtual MimeType   : abap.char( 50 ),
          @ObjectModel.virtualElementCalculatedBy: 'ABAP:ZCL_MM_PR_FORM'
          @Semantics.largeObject:{
            mimeType: 'MimeType',
            fileName: 'FileName',
            contentDispositionPreference: #ATTACHMENT
          }
  virtual Attachment : abap.rawstring( 0 ),
          /* Associations */
          _Item : redirected to composition child ZMM_C_PrItem
}
